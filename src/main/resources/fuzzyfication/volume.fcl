FUNCTION_BLOCK volume

VAR_INPUT
        intensityLevel: REAL;
        dayPart: REAL;
END_VAR

VAR_OUTPUT
        intensityChange: REAL;
END_VAR

FUZZIFY intensityLevel
        TERM veryQuiet := (0, 1) (10, 1) (20, 0.50) (30, 0);
        TERM quiet := (10, 0) (20, 0.50) (30, 1) (40, 1) (50, 0.50) (60, 0);
        TERM loud := (40, 0) (50, 0.50) (60, 1) (70, 1) (80, 0.50) (90, 0);
        TERM veryLoud := (70, 0) (80, 0.50) (90, 1) (100, 1);
END_FUZZIFY

FUZZIFY dayPart
        TERM morning := (1, 0) (3, 0.50) (5, 1) (7, 1) (9, 0.50) (11, 0);
        TERM noon := (7, 0) (9, 0.50) (11, 1) (13, 1) (15, 0.50) (17, 0);
        TERM evening := (13, 0) (15, 0.50) (17, 1) (19, 1) (21, 0.50) (23, 0);
        TERM night := (0,1) (1, 1) (3, 0.50) (5, 0) (19, 0) (21, 0.50) (23, 1);
END_FUZZIFY

DEFUZZIFY intensityChange
        TERM turnDown := (0, 1) (2, 1) (3, 0.50) (4, 0) (7, 0);
        TERM leave := (2, 0) (3, 0.50) (4, 1) (6, 1) (7, 0.50) (8, 0);
        TERM turnUp := (3, 0) (6, 0) (7, 0.50) (8, 1) (10, 1);
        METHOD : COG;
        DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK rules
        AND: MIN;
        ACT: MIN;

        RULE 0: IF intensityLevel IS veryQuiet AND dayPart IS morning THEN intensityChange IS turnUp;
        RULE 1: IF intensityLevel IS quiet AND dayPart IS morning THEN intensityChange IS leave;
        RULE 2: IF intensityLevel IS loud AND dayPart IS morning THEN intensityChange IS leave;
        RULE 3: IF intensityLevel IS veryLoud AND dayPart IS morning THEN intensityChange IS turnDown;

        RULE 4: IF intensityLevel IS veryQuiet AND dayPart IS noon THEN intensityChange IS turnUp;
        RULE 5: IF intensityLevel IS quiet AND dayPart IS noon THEN intensityChange IS turnUp;
        RULE 6: IF intensityLevel IS loud AND dayPart IS noon THEN intensityChange IS leave;
        RULE 7: IF intensityLevel IS veryLoud AND dayPart IS noon THEN intensityChange IS turnDown;

        RULE 8: IF intensityLevel IS veryQuiet AND dayPart IS evening THEN intensityChange IS turnUp;
        RULE 9: IF intensityLevel IS quiet AND dayPart IS evening THEN intensityChange IS leave;
        RULE 10: IF intensityLevel IS loud AND dayPart IS evening THEN intensityChange IS turnDown;
        RULE 11: IF intensityLevel IS veryLoud AND dayPart IS evening THEN intensityChange IS turnDown;

        RULE 12: IF intensityLevel IS veryQuiet AND dayPart IS night THEN intensityChange IS leave;
        RULE 13: IF intensityLevel IS quiet AND dayPart IS night THEN intensityChange IS leave;
        RULE 14: IF intensityLevel IS loud AND dayPart IS night THEN intensityChange IS turnDown;
        RULE 15: IF intensityLevel IS veryLoud AND dayPart IS night THEN intensityChange IS turnDown;
END_RULEBLOCK

END_FUNCTION_BLOCK